group 'com.pauldemarco.flutter_blue'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.15'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
    }
    sourceSets {
        main {
            proto {
                srcDir '../protos'
                include '*.proto'
            }

            java {
                srcDir '../protos'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'src/libs', include: ['*.jar'])
    compileOnly fileTree(dir: 'src/libs', include: ['*.aar'])
    implementation 'com.google.protobuf:protobuf-javalite:3.11.4'
}